# Process this file with autoconf to produce a configure script.

AC_INIT(inetd/inetd.c)

AC_ARG_ENABLE(encryption, enable connection encryption,, enable_encryption=no)
AC_ARG_ENABLE(authentication, enable connection authentication,, enable_authentication=no)

# By default we compile both servers and clients, but see what the user wants
AC_ARG_ENABLE(servers, compile servers by default,, enable_servers=yes)
AC_ARG_ENABLE(clients, compile clients by default,, enable_clients=yes)

AC_PROG_MAKE_SET

AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_YACC
AC_CHECK_TOOL(AR, ar)
AC_CHECK_TOOL(RANLIB, ranlib, :)
AC_PATH_PROG(RM, rm, rm)

AC_CHECK_HEADERS(malloc.h termio.h termios.h sys/tty.h sys/utsname.h \
		 krb.h des.h netinet/ip.h netinet/in_systm.h)
AC_CHECK_FUNCS(cgetent uname strdup)
AC_CHECK_LIB(krb, krb_mk_req)
AC_CHECK_LIB(des, des_key_sched)
AC_CHECK_LIB(util, logout)
AC_TYPE_SIGNAL

AC_CACHE_CHECK(whether struct stat has timespec fields,
	       inetutils_cv_struct_st_timespec,
AC_TRY_COMPILE(
[#include <sys/types.h>
#include <sys/stat.h>],
[struct stat st; st.st_mtimespec.ts_nsec = 0;],
[inetutils_cv_struct_st_timespec=yes],
[inetutils_cv_struct_st_timespec=no]))

if test "$inetutils_cv_struct_st_timespec" = yes; then
  AC_DEFINE(HAVE_ST_TIMESPEC)
else
AC_CACHE_CHECK(whether struct stat has time_usec fields,
	       inetutils_cv_struct_st_time_usec,
AC_TRY_COMPILE(
[#include <sys/types.h>
#include <sys/stat.h>],
[struct stat st; st.st_mtime_usec = 0;],
[inetutils_cv_struct_st_time_usec=yes],
[inetutils_cv_struct_st_time_usec=no]))

if test "$inetutils_cv_struct_st_time_usec" = yes; then
  AC_DEFINE(HAVE_ST_TIME_USEC)
fi
fi

SERVERS_OR_CLIENTS=
if test "$enable_servers" = yes; then
  SERVERS_OR_CLIENTS='$(SERVERS)'
fi
if test "$enable_clients" = yes; then
  SERVERS_OR_CLIENTS="$SERVERS_OR_CLIENTS "'$(CLIENTS)'
fi
AC_SUBST(SERVERS_OR_CLIENTS)

if test "$enable_authentication" = yes; then
  AC_DEFINE(AUTHENTICATION)
fi
if test "$enable_encryption" = yes; then
  AC_DEFINE(ENCRYPTION)
fi

if test "$enable_authentication" = yes -a "$ac_cv_lib_krb" = yes; then
  AC_DEFINE(KRB4)
fi
if test "$enable_encryption" = yes -a "$ac_cv_lib_des" = yes; then
  AC_DEFINE(DES_ENCRYPTION)
fi

AC_OUTPUT(Makefile inetd/Makefile libtelnet/Makefile telnetd/Makefile
	  ftpd/Makefile rshd/Makefile rlogind/Makefile uucpd/Makefile
	  rexecd/Makefile talkd/Makefile
	  telnet/Makefile ftp/Makefile rsh/Makefile rcp/Makefile
	  rlogin/Makefile talk/Makefile)
